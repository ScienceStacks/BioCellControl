# Template file for the Spiro model of chemotaxis in PNAS, 1999.
# The species B, R, T2, R, Y are specified separately.
{{ SetVersion 1.1 }}
{{ DefineVariables Begin }}
MIN_ME = 2  # Minimum methylation
MAX_ME = 4  # Maximum methylation
api.addDefinitions({'l': ['L', ''],  # Ligand binding
                    'm': range(MIN_ME, MAX_ME+1),  # Methylation level
                    'p': ['p', ''],  # Phosphorylation
                    'r': ['R', ''],  # Bound to CheR
                    'me': range(MIN_ME, MAX_ME),  # Increase methylation
                    'de': range(MIN_ME+1, MAX_ME)}) # Decrease methylation
{{ DefineVariables End }}
J0: $x0 -> L; k0*x0
# REACTIONS from Spiro, Table 3
# Methylation
J1_{me}{p}R: T{me}{p}R -> T{me+1}{p} + R; k1c*T{me}{p}R
J1_L{me}{p}R: LT{me}{p}R -> LT{me+1}{p} + R; k3c*LT{me}{p}R
# Demethylations.
J4a_{de}{p}{r}: T{de}{p}{r} + Bp -> T{de-1}{p}{r} + Bp; k_1*T{de}{p}{r}*Bp
J4c_L{de}{p}{r}: LT{de}{p}{r} + Bp -> LT{de-1}{p}{r} + Bp; k_3*LT{de}{p}{r}*Bp
#   Details of *T* + R <-> *T*R
J2_{l}{m}{p}: {l}T{m}{p}R -> {l}T{m}{p} + R; k1a*{l}T{m}{p}R
J3_{l}{m}{p}: {l}T{m}{p} + R -> {l}T{m}{p}R; k1b*{l}T{m}{p}*R
# Ligand binding and unbinding
J5_{m}{p}{r}: L + T{m}{p}{r} -> LT{m}{p}{r}; k5*L*T{m}{p}{r}
J6_{m}{p}{r}: LT{m}{p}{r} -> L + T{m}{p}{r}; k_5*LT{m}{p}{r}
# Autophosphorylations
J7_{m}{r}: T{m}{r} -> T{m}p{r}; k{m+6}*T{m}{r}
J7_L{m}{r}: LT{m}{r} -> LT{m}p{r}; k{m+9}*LT{m}{r}
# B phosphorylations
J8_{l}{m}{r}: {l}T{m}p{r} + B -> {l}T{m}{r} + Bp; kb*{l}T{m}p{r}*B
# Y phosphorylations
J9_{l}{m}{r}: {l}T{m}p{r} + Y -> {l}T{m}{r} + Yp; ky*{l}T{m}p{r}*Y
# B & Y dephosphorylations
J10: Bp -> B; k_b*Bp
J11: Yp + Z -> Y + Z; k_y*Yp*Z    
# Constants from Spiro, Table 3, except k1a (which is noted above).
k1b = k5
k1a = (1.7e-6)/k1b
k2a = k1a
k2b = k1b
k3a = k1a
k3b = k1b
k1c = 0.17
k2c = 0.1*k1c
k3c = 30*k1c
k4c = 30*k2c
k5  = 7e7
k_5 = 70
k_1 = 4e5
k_2 = 3e4
k_3 = k_1
k_4 = k_2
k8 = 15
k9 = 3*k8
k10 = 3.2*k8
k11 = 0
k12 = 1.1*k8
k13 = 0.72*k10
kb = 8e5
ky = 3e7
k_b = 0.35
k_y = 5e5
# Other constants
k0 = 0
x0 = 1  # Used for ramp
{l}T{m}{p}{r} = 0  # Declare variables
B{p} = 0
Y{p} = 0
L = 0
